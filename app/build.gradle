apply plugin: 'com.android.application'

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def commitCount = 100 + Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.hilllander.khunzohn.gpstracker"
        minSdkVersion 15
        targetSdkVersion 23
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        versionCode commitCount
        versionName gitTag
    }
    buildTypes {
        debug {
            resValue "string", "google_maps_api_key", "AIzaSyCOeKXHAYPNFV2LQYb0HdsTFMNZ1ymo3c8"
            versionNameSuffix "-debug" + "-build" + buildTime()
        }
        release {
            resValue "string", "google_maps_api_key", "//TODO replage with release api key"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix "-${gitSha}"
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }
}
ext {
    //version
    supportLibrary = "23.1.1"
    //support dependencies
    supportDependencies = [appCompat   : "com.android.support:appcompat-v7:${supportLibrary}",
                           recyclerView: "com.android.support:recyclerview-v7:${supportLibrary}",
                           cardView    : "com.android.support:cardview-v7:${supportLibrary}",
                           design      : "com.android.support:design:${supportLibrary}"]
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile supportDependencies.appCompat
    compile supportDependencies.recyclerView
    compile supportDependencies.cardView
    compile supportDependencies.design
    compile 'com.google.android.gms:play-services:8.4.0'
    compile project(':mmtext')
    compile('com.github.ozodrukh:CircularReveal:1.1.1@aar') {
        transitive = true;
    }
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'

}
